 #compdef m

function _m_dir {
    local -a opts args
    local delete_help="delete empty folders recursively in the current or specified path"
    local dsfiles_help="restore/prohibit generation of .DS_Store"
    local cmd=dir

    case $CURRENT in
        3)
            args=(
                "tree:tree view of folders in the current or specified path"
                "size:calculate folder size"
                "delete:$delete_help"
                "dsfiles:$dsfiles_help"
                help
            )
            ;;
        4)
            local sub=${words[3]}

            case $sub in
                tree)
                    _path_files
                    ;;
                size)
                    _path_files
                    ;;
                delete)
                    args=(
                        "empty:$delete_help"
                        "dsfiles:delete .DS_Store files recursively"
                    )
                    ;;
                dsfiles)
                    args=(
                        "on:restore generation of .DS_Store"
                        "off:prohibit generation of .DS_Store"
                    )
                    ;;
            esac
            ;;
        5)
            local sub=${words[3]}
            local sub2=${words[4]}

            [[ $sub == delete && $sub2 == empty ]] && _path_files
            ;;
    esac

    _describe "dir commands" opts -- args
}

function _m_disk_verify {
    local sub=$1
    local -a opts args

    case $CURRENT in
        4)
            args=(
                "volume:$sub volume"
                "disk:$sub disk"
            )
            ;;
        5)
            _path_files
            ;;
    esac

    _describe "disk $sub commands" opts -- args
}

function _m_disk_format {
    local -a opts args

    case $CURRENT in
        4)
            args=(
                "MS-DOS:format the entire disk with a windows format (MS-DOS)"
                "ExFAT:format the entire disk (ExFat)"
                "JHFS+:format the entire disk (JHFS+)"
                "volume:format a volume"
            )
            ;;
        *)
            local sub=${words[4]}

            case $sub in
                "MS-DOS"|"ExFAT"|"JHFS+")
                    [[ $CURRENT == 6 ]] && _path_files
                    ;;
                volume)
                    [[ $CURRENT == 5 ]] && args=(MS-DOS ExFAT JHFS+)
                    [[ $CURRENT == 7 ]] && _path_files
                    ;;
            esac
            ;;
    esac

    _describe 'disk format commands' opts -- args
}

function _m_disk_rename {
    if [[ $CURRENT == 4 ]]; then
        local opts
        local choices=(
            $(
                m disk ls \
                | grep -E '^ +[0-9]:' \
                | sed -E 's/^.+[0-9] (B|.B) +//'
            )
        )
        _describe 'disk rename commands' opts -- choices
    fi
}

function _m_disk {
    local -a opts args

    if [[ $CURRENT == 3 ]]; then
        local ls_help="list disks"
        local fs_help="list available filesystems for formatting"
        args=(
            "ls:$ls_help"
            "list:$ls_help"
            "fs:$fs_help"
            "filesystems:$fs_help"
            "info:display information"
            "ejectall:eject all mountable volumes"
            "verify:verify volume or disk"
            "repair:repair volume or disk"
            "format:format a volume or disk"
            "reformat:reformat a volume"
            "rename:rename a volume"
            help
        )

        _describe 'disk commands' opts -- args
        return
    fi
    local sub=${words[3]}

    case $sub in
        ls|list|info|reformat)
            [[ $CURRENT == 4 ]] && _path_files
            ;;
        verify|repair)
            _m_disk_verify $sub
            ;;
        format)
            _m_disk_format
            ;;
        rename)
            _m_disk_rename
            ;;
    esac

}

function _m_yesno {
    local -a opts args
    local sub=$1
    local what=$2
    local desc=$3
    local yes=$4
    local no=$5
    local _type="commands"

    case $CURRENT in
        3)
            args=(
                "$what:$desc"
                help
            )
            ;;
        4)
            if [[ ${words[3]} == $what ]]; then
                sub=$what
                _type="options"
                args=(
                    "$yes"
                    "$no"
                )
            fi
            ;;
    esac

    _describe "$sub $_type" opts -- args
}

function _m_dock {
    local -a opts args

    if [[ $CURRENT == 3 ]]; then
        args=(
            "showdelay:changes how long the Dock takes to show up when auto-hide is enabled"
            "autohide:enable or disable Dock's auto hide feature"
            "autohidedelay:changes how long the Dock takes to detect you want it to show up"
            "magnification:enable or disable magnification"
            "position:change Dock's position"
            "addblankspace:add a blank space(separator) to the dock"
            "additem:add an item to the Dock, e.g. /Applications/Safari.app"
            "addrecentitems:add a stack containg your recent items to the Dock"
            "prune:removes all apps from dock"
            help
        )

        #_describe 'manage dock properties' opts -- args
        #return
    fi
    local sub=${words[3]}

    case $sub in
        autohide|magnification)
            [[ $CURRENT == 4 ]] && args=("" YES NO)
            ;;
        position)
            [[ $CURRENT == 4 ]] && args=("" BOTTOM LEFT RIGHT)
            ;;
    esac
    _describe 'manage dock properties' opts -- args
    return
}

function _m_fan {
    local -a opts args

    if [[ $CURRENT == 3 ]]; then
        args=(
            "speed:view current fan speed"
            "monitor:monitor fan speed"
            help
        ) 
    fi

    _describe 'fan commands' opts -- args
    return
}

function _m_firewall {
    local -a opts args

    case $CURRENT in
        3)
            args=(
                "status:Show status"
                "list:List applications handled by firewall"
                "enable:Enable firewall"
                "disable:Disable firewall"
                "add:Add app to firewall"
                "remove:Remove app to firewall"
                "blockall:Block all incoming connections"
                help
            )
            ;;
        4)
            if [[ ${words[3]} == add || ${words[3]} == remove ]]; then
                _path_files
            fi
            ;;
    esac

    _describe 'firewall commands' opts -- args
}

function _m_gatekeeper {
    local -a opts args

    case $CURRENT in
        3)
            args=(
                "status:gatekeeper status"
                "list:list rules"
                "enable:enable gatekeeper or a rule"
                "disable:disable gatekeeper or a rule"
                "create:Create a rule for an application"
                help
            )
            ;;
        5)
            if [[ ${words[3]} == create ]]; then
                _path_files
            fi
            ;;
    esac

    _describe 'gatekeeper commands' opts -- args
}

function _m_network {
    local -a opts args
    local sub

    case $CURRENT in
        3)
            args=(
                "ls:list network interfaces"
                "location:get or set the current location"
                help
            )
            ;;
        4)
            if [[ ${words[3]} == location ]]; then
                sub=" location"
                args=(
                    "ls:list locations"
                    "create:create a location"
                    "delete:delete a location"
                    "switch:switch location"
                )
            fi
            ;;
    esac

    _describe "network$sub commands" opts -- args
}

function _m_nosleep {
    local -a opts args

    case $CURRENT in
        3)
            args=(
                "until:until timeout, pid or specified script ends"
                help
            )
            ;;
        4)
            if [[ ${words[3]} == until ]]; then
                #_alternative "pids:process IDs:($process)" \
                    _alternative "args:custom args:((pid:'no sleep until a process ends'))" \
                    "files:filenames:{_path_files}"
            fi
            ;;
        5)
            if [[ ${words[4]} == pid ]]; then
                _alternative "args:custom args:($(ps -A -o pid=))"
            fi
            ;;


    esac

    _describe 'nosleep commands' opts -- args
}

function _m_powermode {
    local -a opts args

    if [[ $CURRENT == 3 ]]; then
        args=(
            "battery:show/change power mode on battery"
            "ac:show/change power mode on ac"
            "auto:set global power mode to auto"
            "low:set global power mode to low"
            "high:set global power mode to high"
            help
        )

        #_describe 'manage power mode' opts -- args
        #return
    fi
    local sub=${words[3]}

    case $sub in
        battery|ac)
            [[ $CURRENT == 4 ]] && args=("" auto low high)
            ;;
    esac
    _describe 'manage power mode' opts -- args
    return
}

function _m_user {
    case $CURRENT in
        3)
            _m_solo \
                "user" \
                "ls:list users" \
                "info:display user information" \
                "create:create specified user (interactive)" \
                "delete:delete specified user" \
                help
            ;;
        4)
            local -a opts args
            local sub=${words[3]}

            case $sub in
                info|delete)
                    args=($(m user ls | cut -d' ' -f1))
                    ;;
            esac
            _describe 'users' opts -- args
            ;;
    esac
}

function _m_wallpaper {
    local -a opts args

    case $CURRENT in
        3)
            args=(help)
            _path_files
            ;;
    esac
    _describe 'wallpaper commands' opts -- args
}

function _m_solo {
    local -a opts
    local cmd=$1
    shift
    local args
    args=($@)

    [[ $CURRENT == 3 ]] && _describe "$cmd commands" opts -- args
}

function _m_help {
    _m_solo help help
}

function _m_cmd {
    local sub=$1

    case $sub in
        help)
            ;;
        audio)
            _m_solo \
                $sub \
                "reset: restarts coreaudiod" \
                help
            ;;
        airdrop)
            _m_solo \
                $sub \
                "status:airdrop status" \
                "on:turn on airdrop" \
                "enable:turn on airdrop" \
                "off:turn off airdrop" \
                "disable:turn off airdrop" \
                help
            ;;
        appearance)
            _m_solo \
                $sub \
                "darkmode:manage dark mode" \
                "transparency:manage transparency" \
                "antialiasthreshold:manage anti-alias threshold" \
                "sidebariconsize:manage sidebar icon size" \
                "maincolor:manage main UI color" \
                "highlightcolor:manage UI highlight color"
            ;;
        battery)
            _m_solo $sub "status:get the battery status" help
            ;;
        bluetooth)
            _m_solo \
                $sub \
                "status:bluetooth status" \
                "on:turn on bluetooth" \
                "enable:turn on bluetooth" \
                "off:turn off bluetooth" \
                "disable:turn off bluetooth" \
                help
            ;;
        dir)
            _m_dir
            ;;
        disk)
            _m_disk
            ;;
        display)
            _m_solo \
                $sub \
                "status:status of displays" \
                help
            ;;
        dns)
            _m_solo \
                $sub \
                "list:lists DNS servers" \
                "add:adds DNS server" \
                "flush:flushes local DNS" \
                help
            ;;
        dock)
            _m_dock
            ;;
        fan)
            _m_fan
            ;;
        finder)
            _m_yesno \
                $sub \
                "showhiddenfiles" \
                "manage hidden file visibility" \
                "YES:show hidden files" \
                "NO:don't show hidden files"
            _m_yesno \
                $sub \
                "showextensions" \
                "manage file extension visibility" \
                "YES:show all file extensions" \
                "NO:don't show all file extensions"
            _m_yesno \
                $sub \
                "showdesktop" \
                "manage desktop status" \
                "YES:enable desktop" \
                "NO:disable desktop"
            ;;
        firewall)
            _m_firewall
            ;;
        flightmode)
            _m_solo \
                $sub \
                "on:turn flightmode off" \
                "off:turn flightmode on" \
                help
            ;;
        gatekeeper)
            _m_gatekeeper
            ;;
        group)
            _m_solo \
                $sub \
                "list:list of groups" \
                "info:display group information" \
                "adduser:add an user to a specific group" \
                "removeuser:remove an user from a specific group" \
                "ismember:show if the user is a member of a specific group" \
                help
            ;;
        hostname | info | lock | sleep | usb)
            _m_help
            ;;
        itunes)
            _m_solo \
                $sub \
                "status:show status, current artist and track" \
                "play:start playing iTunes" \
                "pause:pause iTunes" \
                "next:go to the next track" \
                "prev:go to the previous track" \
                "mute:mute volume" \
                "unmute:unmute volume" \
                "vol:increase and decrease volume" \
                "stop:stop iTunes" \
                "quit:quit iTunes" \
                help
            ;;
        localhost)
            _m_solo \
                $sub \
                "ls:list localhost records" \
                "add:add localhost record" \
                "remove:remove localhost record" \
                help
            ;;
        music)
            _m_solo \
                $sub \
                "status:show status, current artist and track" \
                "play:start playing Music" \
                "pause:pause Music" \
                "next:go to the next track" \
                "prev:go to the previous track" \
                "mute:mute volume" \
                "unmute:unmute volume" \
                "vol:increase and decrease volume" \
                "stop:stop Music" \
                "quit:quit Music" \
                help
            ;;
        network)
            _m_network
            ;;
        nosleep)
            _m_nosleep
            ;;
        notification)
            _m_yesno \
                $sub \
                "showcenter" \
                "set if notification center is enabled" \
                "YES:enable notification center" \
                "NO:disable notification center" \
                help
            ;;
        ntp)
            _m_solo \
                $sub \
                "status:status of the network time service" \
                "enable:enable clock to use network time" \
                "disable:disable clock to use network time" \
                "set:set network time server" \
                help
            ;;
        powermode)
            _m_powermode
            ;;
        printer)
            _m_solo \
                $sub \
                "settings:display printer settings" \
                "name:display printer names on system" \
                "queue:display items in printer queue on system" \
                "drivers:display all printer drivers" \
                "web:enable and show web interface" \
                help
            ;;
        restart)
            _m_solo \
                $sub \
                "-f:restart computer without confirmation" \
                "--force:restart computer without confirmation" \
                help
            ;;
        safeboot)
            _m_solo \
                $sub \
                "status:get the boot args" \
                "enable:enable safeboot" \
                "disable:disable safeboot" \
                help
            ;;
        screensaver)
            _m_yesno \
                $sub \
                "askforpassword" \
                "get the current status" \
                "YES:enable password requirement to unlock" \
                "NO:disable password requirement to unlock"
            ;;
        service)
            local ls_help="list all services show information about the specified service"
            _m_solo \
                $sub \
                "--status-all:$ls_help" \
                "--list:$ls_help" \
                "--ls:$ls_help" \
                "start:start a service" \
                "stop:stop a service" \
                "load:load a service" \
                "unload:unload a service" \
                help
            ;;
        shutdown)
            _m_solo \
                $sub \
                "-f:shutdown computer without confirmation" \
                "--force:shutdown computer without confirmation" \
                help
            ;;
        timezone)
            _m_solo \
                $sub \
                "ls:list available timezones" \
                "set:set timezone" \
                help
            ;;
        touchbar)
            _m_solo \
                $sub \
                "reset:reset the touchbar" \
                help
            ;;
        trash)
            _m_solo \
                $sub \
                "status:get trash info" \
                "clean:clean trash" \
                help
            ;;
        user)
            _m_user
            ;;
        update)
            _m_solo \
                $sub \
                "list:list available updates" \
                "install:install update(s)" \
                help
            ;;
        volume)
            _m_solo \
                $sub \
                "up:increase the volume" \
                "down:decrease the volume" \
                "mute:set mute" \
                "unmute:unset mute" \
                "ismute:check the volume status" \
                help
            ;;
        vpn)
            _m_solo \
                $sub \
                "list:list VPN connections" \
                "start:start vpn connection" \
                "stop:stop vpn connection" \
                "status:status vpn connection" \
                help
            ;;
        wallpaper)
            _m_wallpaper
            ;;
        wifi)
            _m_solo \
                $sub \
                "status:wifi status" \
                "scan:scan wifi" \
                "showpassword:show wifi network password" \
                "list:list known networks" \
                "forget:forget a network" \
                "history:wifi connection history" \
                "off:turn off your wifi" \
                "on:turn on your wifi" \
                "connect:join a wifi network" \
                help
            ;;
    esac
}

function _m {
    local -a opts
    local commands
    commands=(
        help
        "--update:update m-cli to the latest version"
        "--uninstall:uninstall m-cli"
        "airdrop:manage airdrop status"
        "battery:get the battery status"
        "bluetooth:manage bluetooth status"
        "dir:show and delete dir trees"
        "disk:manage disks and volumes"
        "display:display settings"
        "dns:manage dns"
        "dock:manage dock"
        "finder:manage finder properties"
        "firewall:manage firewall"
        "flightmode:manage flightmode"
        "gatekeeper:manage gatekeeper (security for apps)"
        "group:manage groups"
        "hostname:displays or set the computer's hostname"
        "info:print macOS operating system version information"
        "itunes:itunes command line control"
        "localhost:manage localhost file"
        "lock:lock session"
        "music:music command line control"
        "network:manage interfaces and locations"
        "nosleep:prevent sleeping"
        "notification:manage the notification settings"
        "ntp:manage the network time service"
        "powermode:manage power mode"
        "printer:display information about the printers"
        "restart:restart computer"
        "safeboot:manage safeboot"
        "screensaver:launch and manage the screensaver"
        "service:manage services"
        "shutdown:shutdown computer"
        "sleep:put the computer to sleep"
        "timezone:manage timezone"
        "touchbar:manage touchbar"
        "trash:manage the trash"
        "usb:list usb devices"
        "user:manage users"
        "update:manage macOS updates"
        "volume:manage the sound's volume"
        "vpn:basic vpn operations, start, stop, status, list"
        "wallpaper:set the wallpaper"
        "wifi:manage the wifi"
    )

    case $CURRENT in
        2)
            _describe 'm commands' opts -- commands
            ;;
        *)
            local sub=${words[2]}
            _m_cmd $sub
            ;;
    esac
}

_m "$@"
